plugins {
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '[6.0,6.2)'
    id 'org.spongepowered.mixin' version '0.7.+'
}

version = "1.0.1"
group = "com.zoritism.valkyrienskiesstoragescompatmod"

base {
    archivesName = mod_id
}

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
    // The mappings can be changed at any time and must be in the following format.
    // Channel:   Version:
    // official   MCVersion             Official field/method names from Mojang mapping files
    // parchment  YYYY.MM.DD-MCVersion  Open community-sourced parameter names and javadocs layered on top of official
    //
    // You must be aware of the Mojang license when using the 'official' or 'parchment' mappings.
    // See more information here: https://github.com/MinecraftForge/MCPConfig/blob/master/Mojang.md
    //
    // Parchment is an unofficial project maintained by ParchmentMC, separate from MinecraftForge
    // Additional setup is needed to use their mappings: https://github.com/ParchmentMC/Parchment/wiki/Getting-Started
    //
    // Use non-default mappings at your own risk. They may not always work.
    // Simply re-run your setup task after changing the mappings to update your workspace.
    mappings channel: mapping_channel, version: mapping_version

    // When true, this property will have all Eclipse/IntelliJ IDEA run configurations run the "prepareX" task for the given run configuration before launching the game.
    // In most cases, it is not necessary to enable.
    // enableEclipsePrepareRuns = true
    // enableIdeaPrepareRuns = true

    // This property allows configuring Gradle's ProcessResources task(s) to run on IDE output locations before launching the game.
    // It is REQUIRED to be set to true for this template to function.
    // See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
    copyIdeResources = true

    // When true, this property will add the folder name of all declared run configurations to generated IDE run configurations.
    // The folder name can be set on a run configuration using the "folderName" property.
    // By default, the folder name of a run configuration is the name of the Gradle project containing it.
    // generateRunFolders = true

    // This property enables access transformers for use in development.
    // They will be applied to the Minecraft artifact.
    // The access transformer file can be anywhere in the project.
    // However, it must be at "META-INF/accesstransformer.cfg" in the final mod jar to be loaded by Forge.
    // This default location is a best practice to automatically put the file in the right place in the final jar.
    // See https://docs.minecraftforge.net/en/latest/advanced/accesstransformers/ for more information.
    // accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        // applies to all the run configs below
        configureEach {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', mod_id

            mods {
                "${mod_id}" {
                    source sourceSets.main
                }
            }
        }

        client {
            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        server {
            property 'forge.enabledGameTestNamespaces', mod_id
            args '--nogui'
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test command.
        gameTestServer {
            property 'forge.enabledGameTestNamespaces', mod_id
        }

        data {
            // example of overriding the workingDirectory set in configureEach above
            workingDirectory project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', mod_id, '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')
        }
    }
}

mixin {
    add sourceSets.main, "valkyrienskiesstoragescompatmod.refmap.json"

    config "valkyrienskiesstoragescompatmod.mixins.json"

    // debug.verbose = true
    // debug.export = true
    // dumpTargetOnFailure = true
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '17'

repositories {
    mavenCentral()

    maven {
        url "https://repo.spongepowered.org/maven"
    }

    maven {
        url "https://maven.valkyrienskies.org"
    }

    maven {
        url "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }

    if (project.hasProperty("vs_maven_url") && project.vs_maven_url) {
        def customMavenUrl = project.vs_maven_url
        def hasCredentials = project.hasProperty("vs_maven_username") && project.hasProperty("vs_maven_password")

        // теперь создаём репозиторий на основе условий
        maven {
            name = "Valkyrien Skies Internal"
            url = customMavenUrl

            if (hasCredentials) {
                credentials {
                    username = project.vs_maven_username
                    password = project.vs_maven_password
                }
            }
        }
    }
}


dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    // Enable the annotations processor for mixins
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // Implementing against SophisticatedStorage and SophisticatedCore
    implementation fg.deobf("curse.maven:sophisticated-storage-619320:6217888")
    implementation fg.deobf("curse.maven:sophisticated-core-618298:6317048")
    implementation fg.deobf("curse.maven:rftoolsutility-342466:5283900")
    implementation fg.deobf("curse.maven:rftoolsbase-326041:5613272")

    implementation files('libs/mcjtylib-1.20-8.0.4.4.jar')

    // Dependencies for Valkyrien Skies and Eureka
    implementation fg.deobf("curse.maven:valkyrienskies-258371:5212232")
    implementation fg.deobf("curse.maven:eureka-654384:5321630")
    implementation fg.deobf("org.joml:joml:1.10.4") { transitive = false }
    implementation fg.deobf("org.joml:joml-primitives:1.10.0") { transitive = false }

    implementation fg.deobf("org.valkyrienskies:valkyrienskies-${rootProject.vs2_mc_version}-common:${rootProject.vs2_version}")
    implementation fg.deobf("org.valkyrienskies:valkyrienskies-${rootProject.vs2_mc_version}-forge:${rootProject.vs2_version}") { transitive = false }


compileOnly "org.valkyrienskies.core:api:${rootProject.vs_core_version}"
    compileOnly "org.valkyrienskies.core:api-game:${rootProject.vs_core_version}"
    compileOnly "org.valkyrienskies.core:util:${rootProject.vs_core_version}"
    compileOnly "org.valkyrienskies.core:impl:${rootProject.vs_core_version}"


    // MixinExtras для дополнительных возможностей миксинов
    implementation "io.github.llamalad7:mixinextras-common:0.2.0"
}


// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
// When "copyIdeResources" is enabled, this will also run before the game launches in IDE environments.
// See https://docs.gradle.org/current/dsl/org.gradle.language.jvm.tasks.ProcessResources.html
tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            minecraft_version: minecraft_version, minecraft_version_range: minecraft_version_range,
            forge_version: forge_version, forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id, mod_name: mod_name, mod_license: mod_license, mod_version: mod_version,
            mod_authors: mod_authors, mod_description: mod_description,
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand replaceProperties + [project: project]
    }
}

// // Example for how to get properties into the manifest for reading at runtime.
// tasks.named('jar', Jar).configure {
//     manifest {
//         attributes([
//                 'Specification-Title'     : mod_id,
//                 'Specification-Vendor'    : mod_authors,
//                 'Specification-Version'   : '1', // We are version 1 of ourselves
//                 'Implementation-Title'    : project.name,
//                 'Implementation-Version'  : project.jar.archiveVersion,
//                 'Implementation-Vendor'   : mod_authors,
//                 'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
//         ])
//     }

//     // This is the preferred method to reobfuscate your jar file
//     finalizedBy 'reobfJar'
// }

// However if you are in a multi-project build, dev time needs unobfed jar files, so you can delay the obfuscation until publishing by doing:
// tasks.named('publish').configure {
//     dependsOn 'reobfJar'
// }

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}